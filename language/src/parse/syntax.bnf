program = function_definition*


(* function *)

function_definition = "fn" identifier "(" parameters ")" "->" type block

parameters = parameter ("," parameter)*

parameter = identifier ":" type


(* statement, expression *)

block = "{" statements expression? "}"

statements = statement*

statement = expression ";"

expression = add_expression

add_expression = mul_expression (("+" | "-") mul_expression)*

mul_expression = unary_expression (("*" | "/") unary_expression)*

unary_expression =
    primary_expression
  | "-" primary_expression

primary_expression =
    literal
  | identifier
  | "(" expression ")"


(* tokens *)

literal = integer

integer = digit+

identifier = letter (letter | digit)*

type = "i32" | "i64"

letter =
    "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
  | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
  | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
  | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
  | "_"

digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
